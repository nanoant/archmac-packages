# Ported from Arch Linux:
# https://git.archlinux.org/svntogit/packages.git/plain/trunk/PKGBUILD?h=packages/llvm

# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=('llvm' 'llvm-libs') # 'llvm-ocaml'
pkgver=9.0.0
pkgrel=1
# _ocaml_ver=4.09.0
arch=('x86_64')
url="https://llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
makedepends=('cmake' 'ninja' 'libffi' 'libxml2' # 'libedit' 'ncurses' 
             # "ocaml=$_ocaml_ver" 'ocaml-ctypes' 'ocaml-findlib'
             'python3-sphinx' 'python3-recommonmark')
options=('staticlibs')
source=(https://releases.llvm.org/$pkgver/llvm-$pkgver.src.tar.xz{,.sig})
sha256sums=('d6a0565cf21f22e9b4353b2eb92622e8365000a9e90a16b09b56f8157eabfe84'
            'SKIP')
validpgpkeys+=('B6C8F98282B944E3B0D5C2530FC3042E345AD05D') # Hans Wennborg <hans@chromium.org>
validpgpkeys+=('474E22316ABF4785A88C6E8EA2C794A986419D8A') # Tom Stellard <tstellar@redhat.com>

prepare() {
  cd "$srcdir/llvm-$pkgver.src"
  mkdir build
  # NOTE: macOS/BSD sed needs explicit -i argument
  sed -i '' '/add_llvm_tool_subdirectory(gold)/d' tools/CMakeLists.txt
}

build() {
  cd "$srcdir/llvm-$pkgver.src/build"

  cmake .. -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/arch \
    -DLLVM_HOST_TRIPLE=$CHOST \
    -DLLVM_BUILD_LLVM_DYLIB=ON \
    -DLLVM_LINK_LLVM_DYLIB=ON \
    -DLLVM_INSTALL_UTILS=ON \
    -DLLVM_ENABLE_RTTI=ON \
    -DLLVM_ENABLE_FFI=ON \
    -DLLVM_BUILD_DOCS=ON \
    -DLLVM_ENABLE_SPHINX=ON \
    -DLLVM_ENABLE_DOXYGEN=OFF \
    -DSPHINX_WARNINGS_AS_ERRORS=OFF \
    -DFFI_INCLUDE_DIR=$(/opt/arch/bin/pkg-config --variable=includedir libffi)
    # -DLLVM_EXPERIMENTAL_TARGETS_TO_BUILD=AVR \
    # -DLLVM_BUILD_TESTS=ON \
    # -DLLVM_BINUTILS_INCDIR=/opt/arch/include
  ninja all
}

check() {
  cd "$srcdir/llvm-$pkgver.src/build"
  # FIXME: Skip test for now to save time
  # ninja check
}

package_llvm() {
  pkgdesc="Collection of modular and reusable compiler and toolchain technologies"
  depends=('llvm-libs' 'perl')
  optdepends=('python-setuptools: for using lit (LLVM Integrated Tester)')

  cd "$srcdir/llvm-$pkgver.src/build"

  DESTDIR="$pkgdir" ninja install

  # Include lit for running lit-based tests in other projects
  pushd ../utils/lit
  python3 setup.py install --root="$pkgdir" -O1
  popd

  # Remove documentation sources
  rm -r "$pkgdir"/opt/arch/share/doc/$pkgname/html/{_sources,.buildinfo}

  # The runtime libraries go into llvm-libs
  mv -f "$pkgdir"/opt/arch/lib/lib{LLVM,LTO}*.dylib* "$srcdir"
  # mv -f "$pkgdir"/opt/arch/lib/LLVMgold.dylib "$srcdir"

  # OCaml bindings go to a separate package
  # rm -rf "$srcdir"/ocaml.{lib,doc}
  # mv "$pkgdir/opt/arch/lib/ocaml" "$srcdir/ocaml.lib"
  # mv "$pkgdir/opt/arch/share/doc/$pkgname/ocaml-html" "$srcdir/ocaml.doc"

  # if [[ $CARCH == x86_64 ]]; then
  #   # Needed for multilib (https://bugs.archlinux.org/task/29951)
  #   # Header stub is taken from Fedora
  #   mv "$pkgdir/opt/arch/include/llvm/Config/llvm-config"{,-64}.h
  #   cp "$srcdir/llvm-config.h" "$pkgdir/opt/arch/include/llvm/Config/llvm-config.h"
  # fi

  install -dm755               "$pkgdir/opt/arch/share/licenses/$pkgname"
  install -m644 ../LICENSE.TXT "$pkgdir/opt/arch/share/licenses/$pkgname/LICENSE"
}

package_llvm-libs() {
  pkgdesc="LLVM runtime libraries"
  depends=('zlib' 'libffi' 'libxml2') # 'gcc-libs' 'libedit' 'ncurses'

  install -d "$pkgdir/opt/arch/lib"
  cp -P \
    "$srcdir"/lib{LLVM,LTO}*.dylib* \
    "$pkgdir/opt/arch/lib/"
    # "$srcdir"/LLVMgold.dylib \

  # Symlink LLVMgold.dylib from /opt/arch/lib/bfd-plugins
  # https://bugs.archlinux.org/task/28479
  # install -d "$pkgdir/opt/arch/lib/bfd-plugins"
  # ln -s ../LLVMgold.dylib "$pkgdir/opt/arch/lib/bfd-plugins/LLVMgold.dylib"

  install -dm755 "$pkgdir/opt/arch/share/licenses/$pkgname"
  install -m644 "$srcdir/llvm-$pkgver.src/LICENSE.TXT" \
    "$pkgdir/opt/arch/share/licenses/$pkgname/LICENSE"
}

# package_llvm-ocaml() {
#   pkgdesc="OCaml bindings for LLVM"
#   depends=('llvm' "ocaml=$_ocaml_ver" 'ocaml-ctypes')

#   install -d "$pkgdir"/{usr/lib,usr/share/doc/$pkgname}
#   cp -a "$srcdir/ocaml.lib" "$pkgdir/opt/arch/lib/ocaml"
#   cp -a "$srcdir/ocaml.doc" "$pkgdir/opt/arch/share/doc/$pkgname/html"

#   install -Dm644 "$srcdir/llvm-$pkgver.src/LICENSE.TXT" \
#     "$pkgdir/opt/arch/share/licenses/$pkgname/LICENSE"
# }

# vim:set ts=2 sw=2 et:
