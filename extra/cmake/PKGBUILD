# Ported from Arch Linux:
# https://git.archlinux.org/svntogit/packages.git/plain/trunk/PKGBUILD?h=packages/cmake

# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=cmake
pkgver=3.15.5
pkgrel=1
pkgdesc='A cross-platform open-source make system'
arch=('x86_64')
url="https://www.cmake.org/"
license=('custom')
makedepends=('python3-sphinx')
source=("https://www.cmake.org/files/v${pkgver%.*}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('c71a50fe864772dbef16ef472c1ead88e8e322c8451bc395c454af9baa7c7eb6e1bd9abdca0745f979fbacf97f1e1ceaa84c0fcc412cf1e3bcd835aff32199b6')

prepare() {
  cd ${pkgname}-${pkgver}
}

build() {
  cd ${pkgname}-${pkgver}

  ./bootstrap --prefix=/opt/arch \
    --mandir=/share/man \
    --datadir=/share/cmake \
    --docdir=/share/doc/cmake \
    --sphinx-man \
    --no-system-libs \
    --system-zlib \
    --system-bzip2 \
    --system-curl \
    --parallel=$(sysctl machdep.cpu.thread_count)
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  vimpath="${pkgdir}/opt/arch/share/vim/vimfiles"
  install -d "${vimpath}"/{indent,syntax}
  ln -s /opt/arch/share/cmake-${pkgver%.*}/editors/vim/indent/cmake.vim \
    "${vimpath}"/indent/
  ln -s /opt/arch/share/cmake-${pkgver%.*}/editors/vim/syntax/cmake.vim \
    "${vimpath}"/syntax/

  install -d "${pkgdir}"/opt/arch/share/emacs/site-lisp/

  install -d -m755  \
    "${pkgdir}"/opt/arch/share/licenses/${pkgname}
  install -m644 Copyright.txt \
    "${pkgdir}"/opt/arch/share/licenses/${pkgname}/LICENSE
}
