# Ported from Arch Linux:
# https://git.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/python-sphinxcontrib-htmlhelp

# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinxcontrib-htmlhelp
pkgname=python3-sphinxcontrib-htmlhelp
pkgver=1.0.2
pkgrel=1
pkgdesc='Sphinx extension which renders HTML help files'
arch=('any')
url=https://github.com/sphinx-doc/sphinxcontrib-htmlhelp
license=('BSD')
makedepends=('python3-setuptools')
# checkdepends=('python3-html5lib' 'python3-pytest' 'python3-sphinx')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('4670f99f8951bd78cd4ad2ab962f798f5618b17675c35c5ac3b2132a14ea8422')

prepare() {
  cd $_name-$pkgver
  rm -r tests/__pycache__
}

build() {
  cd $_name-$pkgver
  python3 setup.py build
}

check() {
  cd $_name-$pkgver
  # FIXME: Cyclic dependency with python3-sphinx, crazy, disabling:
  # pytest
}

package() {
  cd $_name-$pkgver
  python3 setup.py install --prefix=/opt/arch --root="$pkgdir" --optimize=1 --skip-build
  install -dm755        "$pkgdir"/opt/arch/share/licenses/$pkgname 
  install -m644 LICENSE "$pkgdir"/opt/arch/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
