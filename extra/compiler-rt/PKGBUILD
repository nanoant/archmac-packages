# Ported from Arch Linux:
# https://git.archlinux.org/svntogit/packages.git/plain/trunk/PKGBUILD?h=packages/compiler-rt

# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=compiler-rt
pkgver=9.0.0
pkgrel=1
pkgdesc="Compiler runtime libraries for clang"
arch=('x86_64')
url="https://compiler-rt.llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
# depends=('gcc-libs')
makedepends=('llvm' 'cmake' 'ninja' 'python3')
options=('staticlibs')
source=(https://releases.llvm.org/$pkgver/$pkgname-$pkgver.src.tar.xz{,.sig})
sha256sums=('56e4cd96dd1d8c346b07b4d6b255f976570c6f2389697347a6c3dcb9e820d10e'
            'SKIP')
validpgpkeys+=('B6C8F98282B944E3B0D5C2530FC3042E345AD05D') # Hans Wennborg <hans@chromium.org>
validpgpkeys+=('474E22316ABF4785A88C6E8EA2C794A986419D8A') # Tom Stellard <tstellar@redhat.com>

prepare() {
  cd "$srcdir/$pkgname-$pkgver.src"
  mkdir build
}

build() {
  cd "$srcdir/$pkgname-$pkgver.src/build"

  cmake .. -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/arch \
    -DCOMPILER_RT_ENABLE_IOS=OFF # must be disabled currently for Xcode 11
  ninja
}

package() {
  cd "$srcdir/$pkgname-$pkgver.src/build"

  DESTDIR="$pkgdir" ninja install
  install -dm755               "$pkgdir/opt/arch/share/licenses/$pkgname"
  install -m644 ../LICENSE.TXT "$pkgdir/opt/arch/share/licenses/$pkgname/LICENSE"

  mkdir -p "$pkgdir"/opt/arch/lib/clang/$pkgver/{lib,share}
  mv "$pkgdir"/opt/arch/lib/{darwin,clang/$pkgver/lib/}
  mv "$pkgdir"/opt/arch/{share/*.txt,lib/clang/$pkgver/share/}
}

# vim:set ts=2 sw=2 et:
