# Ported from Arch Linux:
# https://git.archlinux.org/svntogit/packages.git/plain/trunk/PKGBUILD?h=packages/python-setuptools

pkgbase=python3-setuptools
pkgname=('python3-setuptools')
_pkgname='setuptools'
pkgver=41.4.0
pkgrel=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('any')
license=('PSF')
url="http://pypi.python.org/pypi/setuptools"
depends=('python3-packaging')
# _checkdeps=('mock' 'pip' 'pytest-fixture-config' 'pytest-flake8'
#             'pytest-runner' 'pytest-virtualenv' 'wheel')
# checkdepends=("${_checkdeps[@]/#/python-}" 'python-paver' 'git')
provides=('python3-distribute')
replaces=('python3-distribute')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pypa/setuptools/archive/v$pkgver.tar.gz")
sha512sums=('399512d01788a669248a68b1f1eccf940b9255a7ccdbfc0575abcbe9866477c2850260fd8fe8fc946cdb2687bbbcc493d963aa48e38b22e4264a1f2b68ed648c')

prepare() {
  cd "$srcdir"/$_pkgname-$pkgver

  # Remove post-release tag since we are using stable tags
  sed -i~ \
      -e '/tag_build = .post/d' \
      -e '/tag_date = 1/d' \
      setup.cfg

  cd "$srcdir"/$_pkgname-$pkgver
  sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python3|" setuptools/command/easy_install.py
}

build() {
  cd "$srcdir"/$_pkgname-$pkgver
  python3 bootstrap.py
  python3 setup.py build
}

check() {
  # Workaround UTF-8 tests by setting LC_CTYPE

  cd "$srcdir"/$_pkgname-$pkgver
  # FIXME: Depends on many extra dependencies, see above, so disabling for now:
  # LC_CTYPE=en_US.utf8 python3 setup.py ptr
}

package() {
  cd "$srcdir"/$_pkgname-$pkgver
  python3 setup.py install --prefix=/opt/arch --root="$pkgdir" --optimize=1 --skip-build
  rm "$pkgdir"/opt/arch/bin/easy_install
}
