# Ported from Arch Linux:
# https://git.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/python-pytest-runner

# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python3-pytest-runner
pkgver=5.2
pkgrel=1
pkgdesc="Invoke py.test as distutils command with dependency resolution"
arch=('any')
license=('MIT')
url="https://github.com/pytest-dev/pytest-runner"
depends=('python3-pytest')
makedepends=('python3-setuptools-scm')
# checkdepends=('python3-pytest-black' 'python3-pytest-cov' 'python3-pytest-flake8')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-runner/archive/$pkgver.tar.gz")
sha512sums=('0ce9c191481ab97795d0f45bdea7669cb61c3ac6aac476954d34c3e09e6fb08fd6bf73f192c014cb7ff2bd90dbfeb0f01d9a9ba7a9c6caeb371e40b42b558d63')

prepare() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pytest-runner-$pkgver
  python3 setup.py build
}

check() {
  cd pytest-runner-$pkgver
  python3 setup.py egg_info
  # Requires extra dependencies 'python3-pytest-black' 'python3-pytest-cov' 'python3-pytest-flake8', so disabling:
  # PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-runner-$pkgver
  python3 setup.py install --prefix=/opt/arch --root="$pkgdir" --optimize=1
  install -dm755        "$pkgdir"/opt/arch/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/opt/arch/share/licenses/$pkgname/LICENSE
}
