# Ported from Arch Linux:
# https://git.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/python-sphinx

# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sébastien Luttringer
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Fabio Volpe <volpefabio@gmail.com>

pkgname=python3-sphinx
pkgver=2.2.1
pkgrel=1
pkgdesc='Python documentation generator'
arch=('any')
url=http://www.sphinx-doc.org/
license=('BSD')
depends=('python3-babel'
         'python3-docutils'
         'python3-imagesize'
         'python3-jinja'
         'python3-pygments'
         'python3-requests'
         'python3-setuptools'
         'python3-snowballstemmer'
         'python3-sphinx-alabaster-theme'
         'python3-sphinxcontrib-'{{apple,dev,html}help,jsmath,qthelp,serializinghtml}
         )
source=("https://pypi.org/packages/source/S/Sphinx/Sphinx-$pkgver.tar.gz"{,.asc})
sha256sums=('31088dfb95359384b1005619827eaee3056243798c62724fd3fa4b84ee4d71bd'
            'SKIP')
validpgpkeys=('8A11B79A5D0D749A66F0A030102C2C17498D6B9E'  # Takeshi KOMIYA
              'E9BEABB07E7B9CC3F56E62C91425F8CE5EBA0E07') # Takayuki Shimizukawa

build() {
  cd Sphinx-$pkgver
  python3 setup.py build
}

check() {
  cd Sphinx-$pkgver
  # FIXME: We miss certain dependencies so 8 tests fail, disabling for now:
  # LC_ALL="en_US.UTF-8" make test
  rm -r tests
}

package() {
  cd Sphinx-$pkgver
  python3 setup.py install --prefix=/opt/arch --root="$pkgdir" --optimize=1 --skip-build
  install -dm755         "$pkgdir"/opt/arch/share/licenses/$pkgname
  install -m644  LICENSE "$pkgdir"/opt/arch/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
