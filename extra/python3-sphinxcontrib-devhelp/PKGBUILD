# Ported from Arch Linux:
# https://git.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/python-sphinxcontrib-devhelp

# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinxcontrib-devhelp
pkgname=python3-sphinxcontrib-devhelp
pkgver=1.0.1
pkgrel=1
pkgdesc='Sphinx extension which outputs Devhelp document'
arch=('any')
url=https://github.com/sphinx-doc/sphinxcontrib-devhelp
license=('BSD')
makedepends=('python3-setuptools')
# checkdepends=('python3-pytest' 'python3-sphinx')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('6c64b077937330a9128a4da74586e8c2130262f014689b4b89e2d08ee7294a34')

build() {
  cd $_name-$pkgver
  python3 setup.py build
}

check() {
  cd $_name-$pkgver
  # FIXME: Cyclic dependency with python3-sphinx, crazy, disabling:
  # pytest
}

package() {
  cd $_name-$pkgver
  python3 setup.py install --prefix=/opt/arch --root="$pkgdir" --optimize=1 --skip-build
  install -dm755        "$pkgdir"/opt/arch/share/licenses/$pkgname 
  install -m644 LICENSE "$pkgdir"/opt/arch/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
