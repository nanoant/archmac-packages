# Ported from Arch Linux:
# https://git.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/python-py

# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Igor Ramos Tiburcio <irtigor@yahoo.com.br>
# Contributor: Wieland Hoffmann <themineo+aur@gmail.com>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python3-py
pkgver=1.8.0
pkgrel=1
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('any')
license=('MIT')
url="https://py.readthedocs.io/"
depends=('python3-iniconfig' 'python3-apipkg')
makedepends=('python3-setuptools' 'python3-setuptools-scm')
checkdepends=('python3-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/py/archive/$pkgver.tar.gz")
sha512sums=('83c7de6cfe89f5064f5f7266739b16adf61fd8f4f77ead787e17e41b7fb4d5f68fa242eab9a04c0c1a88cc7c47e68c78256e4432c5f8eeff5ac3dc36a90cfa3a')

prepare() {
  export LC_CTYPE=en_US.UTF-8
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/py-$pkgver
  python3 setup.py build
}

check() {
  # Known failure: https://github.com/pytest-dev/py/issues/104

  cd "$srcdir"/py-$pkgver
  python3 setup.py pytest || warning "Tests failed"
}

package() {
  cd py-$pkgver
  python3 setup.py install --prefix=/opt/arch --root="$pkgdir" --optimize=1
  install -dm755        "$pkgdir"/opt/arch/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/opt/arch/share/licenses/$pkgname/LICENSE
}
