# Ported from Arch Linux:
# https://git.archlinux.org/svntogit/community.git/tree/trunk/PKGBUILD?h=packages/ninja

# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Filip Brcic <brcha@gna.org>
# Contributor: Mika Fischer <mika.fischer@zoopnet.de>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=ninja
pkgver=1.9.0
pkgrel=1
pkgdesc='Small build system with a focus on speed'
arch=(x86_64)
url='https://ninja-build.org/'
license=(Apache)
makedepends=(re2c)
source=($pkgname-$pkgver.zip::https://github.com/martine/ninja/archive/v$pkgver.zip)
sha1sums=('8dc0fe67796e1c50a00f9542af41021286977bde')
prefix=/opt/arch

build() {
  cd ninja-$pkgver
  python configure.py --bootstrap
}

check() {
  cd ninja-$pkgver

  python ./configure.py
  ./ninja ninja_test
}

package() {
  cd ninja-$pkgver

  install -d -m755       "$pkgdir/$prefix/bin"
  install -m755 ninja    "$pkgdir/$prefix/bin/ninja"
  install -d -m755       "$pkgdir/$prefix/share/doc/ninja"
  install -m644 doc/manual.asciidoc "$pkgdir/$prefix/share/doc/ninja/manual.asciidoc"
  install -d -m755      "$pkgdir/$prefix/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/$prefix/share/licenses/$pkgname/COPYING"

  install -d -m755                  "$pkgdir/$prefix/share/emacs/site-lisp"
  install -m644 misc/ninja-mode.el  "$pkgdir/$prefix/share/emacs/site-lisp/ninja-mode.el"
  install -d -m755                  "$pkgdir/$prefix/share/vim/vimfiles/syntax"
  install -m644 misc/ninja.vim      "$pkgdir/$prefix/share/vim/vimfiles/syntax/ninja.vim"

  install -d -m755                   "$pkgdir/$prefix/share/bash-completion/completions"
  install -m644 misc/bash-completion "$pkgdir/$prefix/share/bash-completion/completions/ninja"
  install -d -m755                   "$pkgdir/$prefix/share/zsh/site-functions"
  install -m644 misc/zsh-completion  "$pkgdir/$prefix/share/zsh/site-functions/_ninja"
}
