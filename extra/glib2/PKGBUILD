# Partially ported from Arch Linux:
# https://git.archlinux.org/svntogit/packages.git/tree/trunk/PKGBUILD?h=packages/glib2

pkgname=glib2
pkgver=2.62.3
pkgrel=1
pkgdesc='Low level core library'
url='https://wiki.gnome.org/Projects/GLib'
license=('LGPL2.1')
arch=('x86_64')
depends=('pcre' 'libffi>=3.0.0' 'zlib>=1.2.10') # NOTE: We need version for libffi and zlib, otherwise pacman does not pull them.
makedepends=('gettext' 'meson' 'ninja' 'pkg-config' 'docbook-xsl')
source=(https://download.gnome.org/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz)
sha256sums=('4400adc9f0d3ffcfe8e84225210370ce3f9853afb81812ddadb685325aa655c4')

build() {
  export XML_CATALOG_FILES=/opt/arch/etc/xml/catalog
  CPPFLAGS=-I/opt/arch/include \
  LDFLAGS=-L/opt/arch/lib \
  meson glib-$pkgver build \
    -D prefix=/opt/arch \
    -D man=true \
    -D gtk_doc=false
  ninja -C build
}

check() {
  # Some tests currently still fail on macOS, so disabling for now.
  # meson test -C build --no-suite flaky --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
