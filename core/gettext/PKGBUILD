# Loosely inspired by Arch Linux:
# https://git.archlinux.org/svntogit/packages.git/tree/trunk/PKGBUILD?h=packages/gettext

pkgname=gettext
# pkgver=0.19.8.1
pkgver=0.20.1
pkgrel=1
pkgdesc="GNU internationalization library"
arch=('x86_64')
url="http://www.gnu.org/software/gettext/"
license=('GPL')
groups=('base' 'base-devel')
depends=()
optdepends=('git: for autopoint infrastructure updates')
options=(!docs)
install=gettext.install
source=(https://ftp.gnu.org/gnu/gettext/${pkgname}-${pkgver}.tar.xz{,.sig}
        'setlocale-mac-less-verbose.patch' # https://git.merproject.org/mirror/gettext/commit/ec0e6b307456ceab352669ae6bccca9702108753.diff'
        # See: https://github.com/Homebrew/homebrew-core/issues/41139
        )
sha256sums=('53f02fbbec9e798b0faaf7c73272f83608e835c6288dd58be6c9bb54624a3800' # 0.20.1
           #'105556dbc5c3fbbc2aa0edb46d22d055748b6f5c7cd7a8d99f8e7eb84e938be4' # 0.19.8.1
            'SKIP'
            'SKIP')
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871'  # Daiki Ueno
              '68D94D8AAEEAD48AE7DC5B904F494A942E4616C2') # Bruno Haible (Open Source Development)

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < "${srcdir}/setlocale-mac-less-verbose.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/opt/arch \
    --disable-dependency-tracking \
    --disable-silent-rules \
    --disable-debug \
    --disable-csharp \
    --disable-native-java \
    --disable-java \
    --disable-examples \
    --with-included-gettext \
    --with-included-glib \
    --with-included-libcroco \
    --with-included-libunistring \
    --without-git \
    --without-cvs \
    --without-xz
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
